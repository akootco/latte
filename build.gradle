plugins {
    id 'java'
    id 'io.github.goooler.shadow' version '8.1.7'
    id "org.jetbrains.kotlin.jvm" version "2.0.0"
}

group = project.property('group')
version = project.property('version')

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    // Paper
    compileOnly 'io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT'

    // Kotlin
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:2.0.0"

    // Modules
    compileOnly project(":bluefox")

    // Tests
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
}

processResources {
    def props = [group: project.property('group'), name: project.property('modulename'), version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    destinationDirectory = file('../test/server/plugins')
    archiveBaseName.set(project.property('modulename') as String)
    archiveClassifier.set('')
    exclude 'META-INF/*.kotlin_module'
    exclude 'kotlin/**'
    exclude 'org/jetbrains/**'
    exclude 'org/intellij/**'
}